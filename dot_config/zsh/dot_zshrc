# 重複するpathを追加しない
typeset -U path cdpath fpath manpath

### history
HISTSIZE=100000
SAVEHIST=1000000
export HISTFILE="$XDG_STATE_HOME/zsh_history"

setopt AUTO_PUSHD
setopt PUSHD_IGNORE_DUPS
setopt GLOBDOTS
setopt APPEND_HISTORY
setopt EXTENDED_HISTORY
setopt HIST_IGNORE_ALL_DUPS
setopt HIST_IGNORE_SPACE
setopt HIST_REDUCE_BLANKS
setopt HIST_SAVE_NO_DUPS
setopt INTERACTIVE_COMMENTS
setopt MAGIC_EQUAL_SUBST
setopt PRINT_EIGHT_BIT
setopt NO_FLOW_CONTROL
setopt INC_APPEND_HISTORY
setopt SHARE_HISTORY
setopt AUTO_PARAM_KEYS
setopt list_packed

# 特定コマンドの実行時に履歴に残さない
zshaddhistory() {
  local line="${1%%$'\n'}"
  [[ ! "$line" =~ "^(cd|jj?|la|ll|ls|rm|rmdir)($| )" ]]
}

# 正常終了以外のコマンドを履歴に残さない
__update_history() {
  local last_status="$?"

  local HISTFILE="$XDG_STATE_HOME/zsh_history"
  fc -W
  if [[ ${last_status} -ne 0 ]]; then
    ed -s ${HISTFILE} <<EOF >/dev/null
d
w
q
EOF
  fi
}
precmd_functions+=(__update_history)

# 補完で小文字でも大文字にマッチさせる
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}'

#manコマンドのエイリアス設定
alias man='env LANG=C man'
alias jman='env LANG=ja_JP.UTF-8 man'

#サジェストの色
ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE="fg=244"

#zshの処理時間測定用
if (which zprof >/dev/null 2>&1); then
  zprof
fi

# Homebrew
eval "$(/opt/homebrew/bin/brew shellenv)"
export PATH=$(brew --prefix openssl)/bin:$PATH
export PATH=$(brew --prefix curl)/bin:$PATH

if type brew &>/dev/null; then
  FPATH="$(brew --prefix)/share/zsh/site-functions:${FPATH}"
  autoload -Uz compinit -C && compinit -C
fi

# pyenvの設定
command -v pyenv >/dev/null || export PATH="$PYENV_ROOT/bin:$PATH"
eval "$(pyenv init -)"
# pyenv-virtualenvの設定(これが重たいので対策したい)
# eval "$(pyenv virtualenv-init -)"

## fzf
function fzf-select-history() {
  BUFFER=$(history -n -r 1 | fzf --query "$LBUFFER")
  CURSOR=$#BUFFER
  zle reset-prompt
}
zle -N fzf-select-history
bindkey '^r' fzf-select-history

### eza
alias ls='eza --group-directories-first'
alias la='eza --group-directories-first -a'
alias ll='eza --group-directories-first -al --header --color-scale --git --icons --time-style=long-iso'
alias tree='eza --group-directories-first -T'

# 自身のIPアドレスを取得
alias myip='curl https://checkip.amazonaws.com/'

export MANPAGER="sh -c 'col -bx | bat --color=always --language=man --plain'"

gcloud-switch() {
  local selected=$(
    gcloud config configurations list --format='table[no-heading](is_active.yesno(yes="[x]",no="[_]"), name, properties.core.account, properties.core.project.yesno(no="(unset)"))' |
      fzf --select-1 --query="$1" |
      awk '{print $2}'
  )
  if [ -n "$selected" ]; then
    gcloud config configurations activate $selected
  fi
}

if [ $DOTFILES/.config/zsh/.zshrc -nt ~/.config/zsh/.zshrc.zwc ]; then
  zcompile ~/.config/zsh/.zshrc
fi

# aws-vaultの補完
eval "$(aws-vault --completion-script-zsh)"
# eval
eval "$(sheldon source)"
eval "$(direnv hook zsh)"
eval "$(starship init zsh)"

if [ -f $XDG_CONFIG_HOME/zsh/.zshrc.work ]; then
  source $XDG_CONFIG_HOME/zsh/.zshrc.work
fi
